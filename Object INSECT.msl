// ---------------------------------------------------------------------------
//  HEMMIS - Ghent University, BIOMATH - Universit√© Laval, modelEAU
//  Implementation: Peter Vanrolleghem, Frederik De Laender, Ludiwine Clouzot.
//  Description: MSL-USER/ELAChao
// ---------------------------------------------------------------------------

#ifndef OBJECTINSECT
#define OBJECTINSECT

CLASS INSECT (* class = "benthic_invertebrate" *) "" EXTENDS LIMITATIONS WITH 
 {:
 interface <- 
 {
 OBJ PD (* terminal = "in_1" *) "" : Concentration := {: causality <- "CIN" :};
 OBJ Bio_algae (* terminal = "in_1" *) "" : Concentration [5;]:={: causality <- "CIN" :};
 OBJ Bio_grazers (* terminal = "in_1" *) "" : Concentration [5;]:={: causality <- "CIN" :};
 OBJ PREDATION_in (* terminal = "in_1" *) "" : RateProcess [5;]:= {: causality <- "CIN" :};
 
 OBJ MORTALITY_out (* terminal = "out_1" *) "" : RateProcess := {: causality <- "COUT" :};
 OBJ EXCRETION_out (* terminal = "out_1" *) "" : RateProcess := {: causality <- "COUT" :};
 OBJ INGESTION_PD_out (* terminal = "out_1" *) "" : RateProcess := {: causality <- "COUT" :};
 OBJ DEFECATION_out (* terminal = "out_1" *) "" : RateProcess := {: causality <- "COUT" :};
 OBJ RESPIRATION_out (* terminal = "out_1" *) "" : RateProcess := {: causality <- "COUT" :};
 OBJ Bio_out (* terminal = "out_1" *) "" : Concentration := {: causality <- "COUT" :};
 OBJ INGESTION_algae_out (* terminal = "out_1" *) "" : RateProcess [5;]:= {: causality <- "COUT" :};
 OBJ INGESTION_grazers_out (* terminal = "out_1" *) "" : RateProcess [5;]:= {: causality <- "COUT" :};
 
 OBJ PROMOTION_out (* terminal = "out_1" *) "" : SRateProcess := {: causality <- "COUT" :}; 
 OBJ RECRUITMENT_in (* terminal = "in_1" *) "" : SRateProcess := {: causality <- "CIN" :};
 OBJ Recruit_out (* terminal = "out_1" *) "" : SRateProcess := {: causality <- "COUT" :}; 
 };
 
 parameters <-
 {
 OBJ Nr "insect nr NECESSARY TO FILL IN" : Real:={:value<-1:};
  
 //INGESTION//
 OBJ CMax "max ingestion rate" : RateCst:={:value<-1.2:};
 
 OBJ Pref_algae "preference for algae nr i" : Real[5;];
 OBJ Pref_grazers "preference for grazer nr i" : Real[5;];
 OBJ Pref_PD "preference for PD" : Real:={:value<-1:};
  
 OBJ FHalfSat_algae "Half saturation ct for ingestion of algae" : Concentration[5;];
 OBJ FHalfSat_grazers "Half saturation ct for ingestion of grazers" : Concentration[5;];
 OBJ FHalfSat_PD "Half saturation ct for ingestion of PD" : Concentration:={:value<-1:};
  
 OBJ Bmin_algae "minimum algae biomass to begin feeding" : Concentration[5;];
 OBJ Bmin_grazers "minimum grazers biomass to begin feeding" : Concentration[5;];
 OBJ Bmin_PD "minimum PD concentration to begin feeding" : Concentration:={:value<-0:};
  
 //DEFECATION//
 OBJ EgestCoeff_algae "egested portion of ingested algae" : Real[5;];
 OBJ EgestCoeff_grazers "egested portion of ingested grazers" : Real[5;];
 OBJ EgestCoeff_PD "egested portion of ingested PD" : Real:={:value<-0:};
  
 //RESPIRATION//
 OBJ RoutineResp "routine respiration rate" : RateCst:={:value<-0.014:};
 OBJ dKResp "respiratory cost for food processing" : Real:={:value<-0.18:}; 
 
 //EXCRETION//
 OBJ KExcr "proportionality constant for excretion:respiration" : Real:={:value<-0.17:};
 
 //MORTALITY//
 OBJ OLIMIT "minimum oxygen concentration" : Concentration:={:value<-1:};
 OBJ KMort "intrinsic mortality rate" : RateCst:={:value<-0.002:};
 
 //PROMOTION//
 OBJ KPro "fraction of growth that goes to promotion. 0 for Instars 3-4" : Real:={:value<-0.5:};
 
 //EMERGENCE//
 OBJ KEm "fraction of growth that goes to emergence. 0 for Instars 1-2" : Real:={:value<-0.5:};
 OBJ EmergenceStart "Date when emergence starts" : Real:={:value<-1:};
 OBJ EmergenceEnd "Date when emergence ends" : Real:={:value<-2:}; 
 
 //RECRUITMENT//
 OBJ PctEggs "fraction of biomass in eggs when emergence. 0 for Instars 1-2" : Real:={:value<-0.09:};
 OBJ EggStart "Date when eggs start being released" : Real:={:value<-1:};
 OBJ EggEnd "Date when eggs end being released" : Real:={:value<-2:};
 };
 
 independent <- { };
 
 state <-
 {
 OBJ Bio "biomass concentration of insect nr i" : SurfaceDensity;
  
 //INGESTION//
 OBJ Food_algae "algae nr i available for feeding" : Concentration[5;];
 OBJ Food_grazers "grazer nr i available for feeding" : Concentration[5;];
 OBJ Food_PD "PD available for feeding" : Concentration;
  
 OBJ INGESTION "INGESTION" : RateProcess;
 OBJ INGESTION_algae "how much this insect eats from algae i" : RateProcess[5;];
 OBJ INGESTION_algae_total "how much this insect eats from algae biomass" : RateProcess;
 OBJ INGESTION_grazers "how much this insect eats from grazer i" : RateProcess[5;];
 OBJ INGESTION_grazers_total "how much this insect eats from grazers biomass" : RateProcess;
 OBJ INGESTION_PD "how much this insect eats from PD" : RateProcess;
   
 OBJ SatFeeding_algae "saturation-algae feeding kinetic factor" : Real[5;];
 OBJ SatFeeding_grazers "saturation-grazers feeding kinetic factor" : Real[5;];
 OBJ SatFeeding_PD "saturation-PD feeding kinetic factor" : Real;
 
 OBJ Help1 "" : Real;
 OBJ Help2 "" : Real;
 OBJ Helpful "" : Real; 
 
 OBJ Preference_algae "preference for algae nr i": Real[5;];
 OBJ PrefAlgae "preference for algae nr i when above Bmin": Real[5;];
 OBJ Pref_algae_total "total preference for algae biomass when above Bmin": Real;
 OBJ Preference_grazers "preference for grazer nr i": Real[5;];
 OBJ PrefGrazers "preference for grazer nr i when above Bmin": Real[5;];
 OBJ Pref_grazers_total "total preference for grazer biomass when above Bmin": Real;
 OBJ Preference_PD "preference for PD": Real;
 OBJ PrefPD "preference for PD when above Bmin": Real;  

 //DEFECATION//
 OBJ DEFECATION "DEFECATION" : RateProcess;
 OBJ DEFECATION_algae "defecation of algae nr i consumed by this insect" : RateProcess[5;];
 OBJ DEFECATION_algae_total "defecation of algae biomass consumed by this insect" : RateProcess;
 OBJ DEFECATION_grazers "defecation of grazer nr i consumed by this insect" : RateProcess[5;];
 OBJ DEFECATION_grazers_total "defecation of grazer biomass consumed by this insect" : RateProcess;
 OBJ DEFECATION_PD "defecation of PD consumed by this grazer" : RateProcess;
  
 //RESPIRATION//
 OBJ RESPIRATION "RESPIRATION" : RateProcess;
 OBJ StdResp "standard respiration" : RateProcess;
 OBJ SpecDynAction "respiration loss for food processing" : RateProcess;
   
 //EXCRETION//
 OBJ EXCRETION "EXCRETION" : RateProcess;
 
 //MORTALITY// 
 OBJ MORTALITY "INTRINSIC MORTALITY" : RateProcess;
 OBJ Dpred "environmental mortality rate" : RateCst;
 
 //PROMOTION//
 OBJ PROMOTION "rate of promotion" : SRateProcess;
 
 //EMERGENCE//
 OBJ EMERGENCE "rate of promotion of Insect Emergence" : SRateProcess;
 OBJ EmergeInsect "rate of Insect Emergence" : SRateProcess;
 
 //RECRUITMENT//
 OBJ Recruit "biomass gained from successful hatching" : SRateProcess;
 };
 
 equations <-
 {
 DERIV(state.Bio,[independent.t])= state.INGESTION - state.DEFECATION - state.RESPIRATION - state.EXCRETION - state.MORTALITY - interface.PREDATION_in[parameters.Nr] + interface.RECRUITMENT_in - state.PROMOTION - state.EmergeInsect;
 
 interface.Bio_out = state.Bio;
 
 {FOREACH count IN {1 .. 5}:interface.INGESTION_algae_out[count] = state.INGESTION_algae[count];};
 {FOREACH count IN {1 .. 5}:interface.INGESTION_grazers_out[count] = state.INGESTION_grazers[count];};
 interface.INGESTION_PD_out = state.INGESTION_PD;	   
 interface.DEFECATION_out = state.DEFECATION; 
 interface.RESPIRATION_out = state.RESPIRATION;
 interface.EXCRETION_out = state.EXCRETION;
 interface.MORTALITY_out = state.MORTALITY;
 interface.PROMOTION_out = state.PROMOTION;
 interface.Recruit_out = state.Recruit;
    
 //INGESTION//
 state.INGESTION = state.INGESTION_algae_total + state.INGESTION_grazers_total + state.INGESTION_PD;
  
 //Algae//
  state.INGESTION_algae_total = SUMOVER count IN {1 .. 5}: state.INGESTION_algae[count];
 
 {FOREACH count IN {1 .. 5}: state.INGESTION_algae[count] = 
 		  IF (parameters.CMax * state.SatFeeding_algae[count] * state.Bio * state.TCorr >= state.Food_algae[count])
 		  THEN state.Food_algae[count]
 		  ELSE parameters.CMax * state.SatFeeding_algae[count] * state.Bio * state.TCorr;};
 
 {FOREACH count IN {1 .. 5}: state.SatFeeding_algae[count] = 
 		  IF (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD <= 0)
		  THEN 0
		  ELSE state.Preference_algae[count] * state.Food_algae[count] / (state.Helpful + parameters.FHalfSat_algae[count]);};
 					 					  
 {FOREACH count IN {1 .. 5}: state.Preference_algae[count] = 
 		  IF (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD <= 0)
		  THEN 0
		  ELSE parameters.Pref_algae[count] / (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD);};
 
 {FOREACH count IN {1 .. 5}:state.PrefAlgae[count] = 
 		  IF (interface.Bio_algae[count] <= parameters.Bmin_algae[count])
		  THEN 0
		  ELSE parameters.Pref_algae[count];};
 
 state.Pref_algae_total = SUMOVER count IN {1 .. 5}: state.PrefAlgae[count];
 
 state.Helpful = state.Help1 + state.Help2 + state.Preference_PD * state.Food_PD;
 
 state.Help1 = SUMOVER count IN {1 .. 5}: state.Food_algae[count] * state.Preference_algae[count];
 
 {FOREACH count IN {1 .. 5}:state.Food_algae[count] = 
 		  IF (interface.Bio_algae[count] <= parameters.Bmin_algae[count])
		  THEN 0
		  ELSE (interface.Bio_algae[count] - parameters.Bmin_algae[count]);};
 
 //Grazers//
  state.INGESTION_grazers_total = SUMOVER count IN {1 .. 5}: state.INGESTION_grazers[count];
 
 {FOREACH count IN {1 .. 5}: state.INGESTION_grazers[count] = 
 		  IF (parameters.CMax * state.SatFeeding_grazers[count] * state.Bio * state.TCorr >= state.Food_grazers[count])
 		  THEN state.Food_grazers[count]
 		  ELSE parameters.CMax * state.SatFeeding_grazers[count] * state.Bio * state.TCorr;};
 
 {FOREACH count IN {1 .. 5}: state.SatFeeding_grazers[count] = 
 		  IF (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD <= 0)
		  THEN 0
		  ELSE state.Preference_grazers[count] * state.Food_grazers[count] / (state.Helpful + parameters.FHalfSat_grazers[count]);};
 					 					  
 {FOREACH count IN {1 .. 5}: state.Preference_grazers[count] = 
 		  IF (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD <= 0)
		  THEN 0
		  ELSE parameters.Pref_grazers[count] / (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD);};
 
 {FOREACH count IN {1 .. 5}:state.PrefGrazers[count] = 
 		  IF (interface.Bio_grazers[count] <= parameters.Bmin_grazers[count])
		  THEN 0
		  ELSE parameters.Pref_grazers[count];};
 
 state.Pref_grazers_total = SUMOVER count IN {1 .. 5}: state.PrefGrazers[count];
 
 state.Helpful = state.Help1 + state.Help2 + state.Preference_PD * state.Food_PD;
 
 state.Help2 = SUMOVER count IN {1 .. 5}: state.Food_grazers[count] * state.Preference_grazers[count];
 
 {FOREACH count IN {1 .. 5}:state.Food_grazers[count] = 
 		  IF (interface.Bio_grazers[count] <= parameters.Bmin_grazers[count])
		  THEN 0
		  ELSE (interface.Bio_grazers[count] - parameters.Bmin_grazers[count]);};

 //PD//
 state.INGESTION_PD = IF (parameters.CMax * state.SatFeeding_PD * state.Bio * state.TCorr >= state.Food_PD)
 		  			  THEN state.Food_PD
 		  			  ELSE parameters.CMax * state.SatFeeding_PD * state.Bio * state.TCorr;
 
 state.SatFeeding_PD = state.Preference_PD * state.Food_PD / (state.Helpful + parameters.FHalfSat_PD);
 
 state.Preference_PD = IF (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD <= 0)
		  			   THEN 0
		  			   ELSE parameters.Pref_PD / (state.Pref_algae_total + state.Pref_grazers_total + state.PrefPD);
					   
 state.PrefPD = IF (interface.PD <= parameters.Bmin_PD)
	 	  		 THEN 0
	 	  		 ELSE parameters.Pref_PD;
 
 state.Food_PD = IF (interface.PD <= parameters.Bmin_PD)
	 	  	     THEN 0
	 	  		 ELSE(interface.PD - parameters.Bmin_PD);
 
 //DEFECATION//
 state.DEFECATION = state.DEFECATION_algae_total + state.DEFECATION_grazers_total + state.DEFECATION_PD; 
  
 state.DEFECATION_algae_total = SUMOVER count IN {1 .. 5}: state.DEFECATION_algae[count];
 
 {FOREACH count IN {1 .. 5}: state.DEFECATION_algae[count] = parameters.EgestCoeff_algae[count] * state.INGESTION_algae[count];};
 
 state.DEFECATION_grazers_total = SUMOVER count IN {1 .. 5}: state.DEFECATION_grazers[count];
 
 {FOREACH count IN {1 .. 5}: state.DEFECATION_grazers[count] = parameters.EgestCoeff_grazers[count] * state.INGESTION_grazers[count];};
 
 state.DEFECATION_PD = parameters.EgestCoeff_PD * state.INGESTION_PD;
 
 //RESPIRATION//
 state.RESPIRATION = (state.StdResp + state.SpecDynAction);
 
 state.StdResp = parameters.RoutineResp * state.TCorr * state.Bio;
 
 state.SpecDynAction = parameters.dKResp * (state.INGESTION - state.DEFECATION);
			 
 //EXCRETION//
 state.EXCRETION = parameters.KExcr * state.RESPIRATION;
 
 //MORTALITY//
 state.MORTALITY = IF (parameters.oxygen_water <= parameters.OLIMIT)
 				   THEN 1 * state.Bio
 				   ELSE
 				   	   IF (state.Dpred > 1)
                	   THEN	1 * state.Bio
		    		   ELSE (state.Dpred) * state.Bio;
 				
 state.Dpred = IF (parameters.Temperature > parameters.TMax)
 			   THEN parameters.KMort +  exp(parameters.Temperature - parameters.TMax)/2 
			   ELSE (parameters.KMort);
 
  //PROMOTION//
 state.PROMOTION = IF ((state.INGESTION - state.DEFECATION - state.RESPIRATION - state.EXCRETION)<= 0)
 				   THEN 0
				   ELSE parameters.KPro * (state.INGESTION - state.DEFECATION - state.RESPIRATION - state.EXCRETION);
				   
 //EMERGENCE//
 state.EMERGENCE = IF ((state.INGESTION - state.DEFECATION - state.RESPIRATION - state.EXCRETION)<= 0)
 				   THEN 0
				   ELSE parameters.KEm * (state.INGESTION - state.DEFECATION - state.RESPIRATION - state.EXCRETION); 
 
 state.EmergeInsect = IF ((parameters.EmergenceStart <= independent.t) && (independent.t <= parameters.EmergenceEnd))
 				  	THEN state.EMERGENCE
					ELSE 0; 

 //RECRUITMENT//
 state.Recruit = IF ((parameters.EggStart <= independent.t) && (independent.t <= parameters.EggEnd))
 				  	THEN parameters.PctEggs * state.EMERGENCE
					ELSE 0;
 };
 :};
 #endif
